-- Aleksander Matusiak
-- EBNF grammar of Matal language

entrypoints Program, Stm, Exp ;

Progr.  Program ::= [External_declaration] ;

(:[]).    [External_declaration] ::= External_declaration;
(:).      [External_declaration] ::= External_declaration [External_declaration];

Afunc.  External_declaration ::= Function_def ;
Global. External_declaration ::= Dec ;

Declarator.      Declarator ::= Type_specifier Ident ;
Declaration.     Dec ::= Declarator ";";

(:[]).   [Dec] ::= Dec ;
(:).     [Dec] ::= Dec [Dec] ;

Tvoid.        Type_specifier ::= "void";
Tint.         Type_specifier ::= "int";
Tbool.        Type_specifier ::= "bool";
Tstruct.      Type_specifier ::= Struct_spec;

Struct.      Struct_spec ::= "struct" Ident "{" [Dec] "}" ;

(:[]).   [Ident] ::= Ident ;
(:).     [Ident] ::= Ident "," [Ident] ;

Func.   Function_def ::= Type_specifier Ident "(" Parameter_declarations ")" Compound_stm;

ParamDec.     Parameter_declarations ::= Declarator ;
MoreParamDec. Parameter_declarations ::= Parameter_declarations "," Parameter_declaration ;

CompS.    Stm ::= Compound_stm ;
ExprS.    Stm ::= Expression_stm;
SelS.     Stm ::= Selection_stm;
IterS.    Stm ::= Iter_stm;
JumpS.    Stm ::= Jump_stm;
PrintS.   Stm ::= Print_stm;

ScompOne.   Compound_stm ::= "{" "}" ;
ScompTwo.   Compound_stm ::= "{" [Stm] "}" ;
ScompThree.  Compound_stm ::= "{" [Dec] [Stm] "}" ;

SexprOne.   Expression_stm ::= ";" ;
SexprTwo.   Expression_stm ::= Exp ";" ;

SselOne.    Selection_stm ::= "if" "(" Exp ")" Stm ;
SselTwo.    Selection_stm ::= "if" "(" Exp ")" Stm "else" Stm ;

SiterOne.   Iter_stm ::= "while" "(" Exp ")" Stm;
SiterThree. Iter_stm ::= "for" "(" Expression_stm Expression_stm ")" Stm ;
SiterFour.  Iter_stm ::= "for" "(" Expression_stm Expression_stm Exp ")" Stm;

SjumpFive.  Jump_stm ::= "return" Exp ";" ;

Sprint.     Print_stm ::= "print" Exp ";" ;

(:[]).   [Stm] ::= Stm ;
(:).     [Stm] ::= Stm [Stm];

Ecomma.       Exp ::= Exp "," Exp1;
Eassign.      Exp1 ::= Exp4 Assignment_op Exp1;
Eplus.        Exp2 ::= Exp2 "+" Exp3;
Eminus.       Exp2 ::= Exp2 "-" Exp3;
Etimes.       Exp3 ::= Exp3 "*" Exp4;
Ediv.         Exp3 ::= Exp3 "/" Exp4;
Eselect.       Exp4 ::= Exp4 "." Ident;
Earray.       Exp4 ::= Exp4 "[" Exp "]" ;
Emap.         Exp4 ::= Exp4 "<" Exp ">" ;
Epostinc.     Exp5 ::= Exp5 "++";
Epostdec.     Exp5 ::= Exp5 "--";
Evar.         Exp6 ::= Ident;

_. Exp ::= Exp1;
_. Exp1 ::= Exp2;
_. Exp2 ::= Exp3;
_. Exp3 ::= Exp4;
_. Exp4 ::= Exp5;
_. Exp5 ::= Exp6;
_. Exp6 ::= "(" Exp ")";

Assign.       Assignment_op ::= "=" ;
AssignMul.    Assignment_op ::= "*=" ;
AssignDiv.    Assignment_op ::= "/=" ;
AssignAdd.    Assignment_op ::= "+=" ;
AssignSub.    Assignment_op ::= "-=" ;
