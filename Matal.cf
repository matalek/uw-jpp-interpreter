-- Aleksander Matusiak
-- EBNF grammar of Matal language

entrypoints Program;

Progr.  Program ::= [ExternalDeclaration] ;

(:[]).    [ExternalDeclaration] ::= ExternalDeclaration;
(:).      [ExternalDeclaration] ::= ExternalDeclaration [ExternalDeclaration];

Afunc.      ExternalDeclaration ::= FunctionDef ;
Global.     ExternalDeclaration ::= Dec ;
StructDec.  ExternalDeclaration ::= StructSpec;

DVariable.      Declarator ::= TypeSpecifier Ident ;

Declaration.     Dec ::= Declarator ";";

[].   [Dec] ::= ;
(:).     [Dec] ::= Dec [Dec] ;

TVoid.        TypeSpecifier ::= "void";
TInt.         TypeSpecifier ::= "int";
TBool.        TypeSpecifier ::= "bool";
TStruct.      TypeSpecifier ::= "struct" Ident;
TArray.       TypeSpecifier ::= TypeSpecifier "[]" ;
TMap.         TypeSpecifier ::= TypeSpecifier "<<" TypeSpecifier ">>" ;

Struct.      StructSpec ::= "struct" Ident "{" [Dec] "}" ;

FuncParams.   FunctionDef ::= "function" Declarator "(" [Declarator] ")" FunctionBody;

separator Declarator ",";
separator FunctionDef "";

FuncBodyOne.  FunctionBody ::= "{" [Dec] [FunctionDef] [Stmt] "return" ExpressionStmt "}" ;

SComp.    Stmt ::= CompoundStmt ;
SExpr.    Stmt ::= ExpressionStmt;
SSel.     Stmt ::= SelectionStmt;
SIter.    Stmt ::= IterStmt;
SPrint.   Stmt ::= PrintStmt;
SInit.    Stmt ::= InitStmt;

SCompOne.  CompoundStmt ::= "{" [Dec] [Stmt] "}" ;

SExprOne.   ExpressionStmt ::= ";" ;
SExprTwo.   ExpressionStmt ::= Exp ";" ;

SSelOne.    SelectionStmt ::= "if" "(" Exp ")" CompoundStmt ;
SSelTwo.    SelectionStmt ::= "if" "(" Exp ")" CompoundStmt "else" CompoundStmt ;

SIterOne.   IterStmt ::= "while" "(" Exp ")" Stmt;
SIterTwo. IterStmt ::= "for" "(" ExpressionStmt ExpressionStmt ")" Stmt ;
SIterThree.  IterStmt ::= "for" "(" ExpressionStmt ExpressionStmt Exp ")" Stmt;

SPrintOne.     PrintStmt ::= "print" Exp ";" ;

SInitOne.      InitStmt ::= Exp "init" Exp ";" ;

[].   [Stmt] ::= ;
(:).     [Stmt] ::= Stmt [Stmt];

EComma.       Exp ::= Exp "," Exp1;
EAssign.      Exp1 ::= Exp4 AssignmentOp Exp1;
EEq.          Exp2  ::= Exp2 "==" Exp3;
ENeq.         Exp2  ::= Exp2 "!=" Exp3;
ELthen.       Exp3 ::= Exp3 "<" Exp4;
EGrthen.      Exp3 ::= Exp3 ">" Exp4;
ELe.          Exp3 ::= Exp3 "<=" Exp4;
EGe.          Exp3 ::= Exp3 ">=" Exp4;
EPlus.        Exp4 ::= Exp4 "+" Exp5;
EMinus.       Exp4 ::= Exp4 "-" Exp5;
ETimes.       Exp5 ::= Exp5 "*" Exp6;
EDiv.         Exp5 ::= Exp5 "/" Exp6;
ENegative.    Exp6 ::= "-" Exp6;
ESelect.      Exp7 ::= Exp7 "." Ident;
EArray.       Exp7 ::= Exp7 "[" Exp "]" ;
EFunk.        Exp7 ::= Exp7 "(" ")";
EFunkPar.     Exp7 ::= Exp7 "(" [Exp1] ")";
EMap.         Exp7 ::= Exp7 "<<" Exp ">>" ;
EPostInc.     Exp8 ::= Exp8 "++";
EPostDec.     Exp8 ::= Exp8 "--";
EVar.         Exp9 ::= Ident;
EConst.       Exp9 ::= Constant;

EInt.         Constant ::= Integer;
ETrue.        Constant ::= "true";
EFalse.        Constant ::= "false";

_. Exp ::= Exp1;
_. Exp1 ::= Exp2;
_. Exp2 ::= Exp3;
_. Exp3 ::= Exp4;
_. Exp4 ::= Exp5;
_. Exp5 ::= Exp6;
_. Exp6 ::= Exp7;
_. Exp7 ::= Exp8;
_. Exp8 ::= Exp9;
_. Exp9 ::= "(" Exp ")";

(:[]).   [Exp1] ::= Exp1 ;
(:).     [Exp1] ::= Exp1 "," [Exp1];

Assign.       AssignmentOp ::= "=" ;
AssignMul.    AssignmentOp ::= "*=" ;
AssignDiv.    AssignmentOp ::= "/=" ;
AssignAdd.    AssignmentOp ::= "+=" ;
AssignSub.    AssignmentOp ::= "-=" ;

comment "/*" "*/" ;
comment "//";
